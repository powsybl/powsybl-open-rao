#
# Copyright (c) 2023, RTE (http://www.rte-france.com)
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.
#

# query: contingencyEquipment
PREFIX cim: <http://iec.ch/TC57/CIM100#>
SELECT *
WHERE {
    ?contingencyEquipment
        rdf:type cim:ContingencyEquipment ;
        cim:ContingencyEquipment.Equipment ?contingencyEquipmentId ;
        cim:ContingencyElement.Contingency ?contingency ;
        cim:ContingencyEquipment.contingentStatus ?contingentStatus ;
}

# query: ordinaryContingency
PREFIX cim: <http://iec.ch/TC57/CIM100#>
PREFIX nc: <http://entsoe.eu/ns/nc#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
SELECT *
{
GRAPH <%s> {
    ?fullModel
        dcat:Model.startDate ?startDate ;
        dcat:Model.endDate ?endDate ;
        dcat:Model.keyword ?keyword .
    ?contingency
        rdf:type nc:OrdinaryContingency ;
        cim:IdentifiedObject.name ?name ;
        nc:Contingency.EquipmentOperator ?idEquipmentOperator ;
        nc:Contingency.normalMustStudy ?normalMustStudy ;
}
}

# query: exceptionalContingency
PREFIX cim: <http://iec.ch/TC57/CIM100#>
PREFIX nc: <http://entsoe.eu/ns/nc#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
SELECT *
{
GRAPH <%s> {
    ?fullModel
        dcat:Model.startDate ?startDate ;
        dcat:Model.endDate ?endDate ;
        dcat:Model.keyword ?keyword .
    ?contingency
        rdf:type nc:ExceptionalContingency ;
        cim:IdentifiedObject.name ?name ;
        nc:Contingency.EquipmentOperator ?idEquipmentOperator ;
        nc:Contingency.normalMustStudy ?normalMustStudy ;
}
}

# query: outOfRangeContingency
PREFIX cim: <http://iec.ch/TC57/CIM100#>
PREFIX nc: <http://entsoe.eu/ns/nc#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
SELECT *
{
GRAPH <%s> {
    ?fullModel
        dcat:Model.startDate ?startDate ;
        dcat:Model.endDate ?endDate ;
        dcat:Model.keyword ?keyword .
    ?contingency
        rdf:type nc:OutOfRangeContingency ;
        cim:IdentifiedObject.name ?name ;
        nc:Contingency.EquipmentOperator ?idEquipmentOperator ;
        nc:Contingency.normalMustStudy ?normalMustStudy ;
}
}

# query: assessedElement
PREFIX cim: <http://iec.ch/TC57/CIM100#>
PREFIX nc: <http://entsoe.eu/ns/nc#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
SELECT *
{
GRAPH <%s> {
    ?fullModel
        dcat:Model.startDate ?startDate ;
        dcat:Model.endDate ?endDate ;
        dcat:Model.keyword ?keyword .
    ?assessedElement
        rdf:type nc:AssessedElement ;
        nc:AssessedElement.inBaseCase ?inBaseCase ;
        cim:IdentifiedObject.name ?name ;
        nc:AssessedElement.AssessedSystemOperator ?assessedSystemOperator ;
        nc:AssessedElement.OperationalLimit ?operationalLimit .
    OPTIONAL {
        ?assessedElement
            nc:AssessedElement.isCritical ?isCritical ;
            nc:AssessedElement.normalEnabled ?normalEnabled ;
            nc:AssessedElement.isCombinableWithRemedialAction ?isCombinableWithRemedialAction ;
            nc:AssessedElement.isCombinableWithContingency ?isCombinableWithContingency .
    }
}
}

# query: assessedElementWithRemedialAction
PREFIX nc: <http://entsoe.eu/ns/nc#>
SELECT *
WHERE {
    ?assessedElementWithRemedialAction
        rdf:type nc:AssessedElementWithRemedialAction ;
        nc:AssessedElementWithRemedialAction.RemedialAction ?remedialAction ;
        nc:AssessedElementWithRemedialAction.AssessedElement ?assessedElement ;
        nc:AssessedElementWithRemedialAction.combinationConstraintKind ?combinationConstraintKind .
    OPTIONAL {
        ?assessedElementWithRemedialAction
            nc:AssessedElementWithRemedialAction.normalEnabled ?normalEnabled .
    }
}

# query: assessedElementWithContingency
PREFIX nc: <http://entsoe.eu/ns/nc#>
SELECT *
WHERE {
    ?assessedElementWithContingency
        rdf:type nc:AssessedElementWithContingency ;
        nc:AssessedElementWithContingency.Contingency ?contingency ;
        nc:AssessedElementWithContingency.AssessedElement ?assessedElement ;
        nc:AssessedElementWithContingency.combinationConstraintKind ?combinationConstraintKind .
    OPTIONAL {
        ?assessedElementWithContingency
            nc:AssessedElementWithContingency.normalEnabled ?normalEnabled .
    }
}

# query: currentLimit
PREFIX cim: <http://iec.ch/TC57/CIM100#>
PREFIX eu: <http://iec.ch/TC57/CIM100-European#>
SELECT *
WHERE {
    ?currentLimit
        rdf:type cim:CurrentLimit ;
        cim:CurrentLimit.normalValue ?normalValue ;
        cim:OperationalLimit.OperationalLimitSet ?operationalLimitSet ;
        cim:OperationalLimit.OperationalLimitType ?operationalLimitType .
    ?operationalLimitSet
        rdf:type cim:OperationalLimitSet ;
        cim:OperationalLimitSet.Terminal ?terminal .
    ?operationalLimitType
        rdf:type cim:OperationalLimitType ;
        eu:OperationalLimitType.kind ?kind ;
        cim:OperationalLimitType.direction ?direction .
    OPTIONAL {
            ?operationalLimitType
                cim:OperationalLimitType.acceptableDuration ?acceptableDuration .
        }
}

# query: gridStateAlterationRemedialAction
PREFIX cim: <http://iec.ch/TC57/CIM100#>
PREFIX nc: <http://entsoe.eu/ns/nc#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>

SELECT *
{
    GRAPH <%s> {
        ?fullModel
            dcat:Model.startDate ?startDate ;
            dcat:Model.endDate ?endDate ;
            dcat:Model.keyword ?keyword .
        ?gridStateAlterationRemedialAction
            rdf:type nc:GridStateAlterationRemedialAction ;
            cim:IdentifiedObject.mRID ?mRID ;
            nc:RemedialAction.normalAvailable ?normalAvailable ;
            nc:RemedialAction.kind ?kind .

        OPTIONAL {
            ?gridStateAlterationRemedialAction cim:IdentifiedObject.name ?name .
        }
        OPTIONAL {
            ?gridStateAlterationRemedialAction nc:RemedialAction.timeToImplement ?timeToImplement .
        }
        OPTIONAL {
            ?gridStateAlterationRemedialAction nc:RemedialAction.RemedialActionSystemOperator ?tso .
        }
    }
}

# query: contingencyWithRemedialAction
PREFIX nc: <http://entsoe.eu/ns/nc#>
SELECT *
WHERE {
    ?contingencyWithRemedialAction
        rdf:type nc:ContingencyWithRemedialAction ;
        nc:ContingencyWithRemedialAction.mRID ?mRID ;
        nc:ContingencyWithRemedialAction.RemedialAction ?gridStateAlterationRemedialAction ;
        nc:ContingencyWithRemedialAction.Contingency ?contingency ;
        nc:ContingencyWithRemedialAction.combinationConstraintKind ?combinationConstraintKind ;
}


# query: topologyAction
PREFIX nc: <http://entsoe.eu/ns/nc#>
PREFIX cim: <http://iec.ch/TC57/CIM100#>
SELECT *
WHERE {
    ?topologyAction
        rdf:type nc:TopologyAction ;
        cim:IdentifiedObject.mRID ?mRID ;
        nc:GridStateAlteration.GridStateAlterationRemedialAction ?gridStateAlterationRemedialAction ;
        nc:TopologyAction.Switch ?switchId ;
        nc:GridStateAlteration.PropertyReference ?propertyReference ;
}

# query: rotatingMachineAction
PREFIX nc: <http://entsoe.eu/ns/nc#>
PREFIX cim: <http://iec.ch/TC57/CIM100#>
SELECT *
WHERE {
    ?rotatingMachineAction
        rdf:type nc:RotatingMachineAction ;
        cim:IdentifiedObject.mRID ?mRID ;
        nc:GridStateAlteration.PropertyReference ?propertyReference ;
        nc:GridStateAlteration.GridStateAlterationRemedialAction ?gridStateAlterationRemedialAction ;
        nc:GridStateAlteration.normalEnabled ?normalEnabled ;
        nc:RotatingMachineAction.RotatingMachine ?rotatingMachineId ;
}

# query: staticPropertyRange
PREFIX nc: <http://entsoe.eu/ns/nc#>
PREFIX cim: <http://iec.ch/TC57/CIM100#>
SELECT *
WHERE {
    ?staticPropertyRange
        rdf:type nc:StaticPropertyRange ;
        cim:IdentifiedObject.mRID ?mRID ;
        nc:RangeConstraint.normalValue ?normalValue ;
        nc:RangeConstraint.valueKind ?valueKind ;
        nc:RangeConstraint.direction ?direction ;
        nc:RangeConstraint.GridStateAlteration ?gridStateAlterationRemedialAction ;
        nc:StaticPropertyRange.PropertyReference ?propertyReference;
}

# query: tapPositionAction
PREFIX nc: <http://entsoe.eu/ns/nc#>
PREFIX cim: <http://iec.ch/TC57/CIM100#>
SELECT *
WHERE {
    ?tapPositionAction
        rdf:type nc:TapPositionAction ;
        cim:IdentifiedObject.mRID ?mRID ;
        nc:GridStateAlteration.PropertyReference ?propertyReference;
        nc:GridStateAlteration.GridStateAlterationRemedialAction ?gridStateAlterationRemedialAction ;
        nc:GridStateAlteration.normalEnabled ?normalEnabled ;
        nc:TapPositionAction.TapChanger ?tapChangerId ;
}
